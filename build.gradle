group 'com.google.cloud.dataflow.sdk.io.kinesis'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'findbugs'
apply plugin: 'checkstyle'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
    mavenLocal()
}

sourceSets {
    test {
        java {
            srcDir "src/test/unit/java"
        }
    }

    integration {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir 'src/test/integration/java'
        }
    }

    e2e {
        java {
            srcDir 'src/test/e2e/java'
            compileClasspath += main.output + test.output + integration.output
            runtimeClasspath += main.output + test.output + integration.output
        }
    }
}

configurations {
    integrationCompile.extendsFrom testCompile
    integrationRuntime.extendsFrom testRuntime

    e2eCompile.extendsFrom integrationCompile
    e2eRuntime.extendsFrom integrationRuntime
}

task integration(type: Test) {
    testClassesDir = sourceSets.integration.output.classesDir
    classpath = sourceSets.integration.runtimeClasspath
}

task e2e(type: Test) {
    testClassesDir = sourceSets.e2e.output.classesDir
    classpath = sourceSets.e2e.runtimeClasspath
}


if (!project.hasProperty("dataflowVersion")) {
    ext.dataflowVersion = 'apache'
}

def dataflowJarMap = ['google': 'com.google.cloud.dataflow:google-cloud-dataflow-java-sdk-all:1.5.0',
                      'apache': 'org.apache.beam:java-sdk-all:0.1.0-incubating-SNAPSHOT']

dependencies {
    compile dataflowJarMap.get(dataflowVersion)
    compile 'com.amazonaws:aws-java-sdk-kinesis:1.10.40'
    compile 'com.amazonaws:amazon-kinesis-client:1.6.1'


    e2eCompile 'com.google.apis:google-api-services-compute:v1-rev91-1.21.0'

    integrationCompile 'org.testng:testng:6.9.10'
    integrationCompile('com.amazonaws:amazon-kinesis-producer:0.10.2') {
        exclude module: 'aws-java-sdk-core'
    }
    integrationCompile 'org.slf4j:slf4j-simple:1.7.13'
    integrationCompile 'com.amazonaws:aws-java-sdk-sts:1.10.40'

    testCompile 'org.easytesting:fest-assert:1.4'
    testCompile('org.mockito:mockito-core:1.10.19') {
        exclude module: 'hamcrest-core'
    }
    testCompile 'org.hamcrest:java-hamcrest:2.0.0.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

idea {
    module {
        testSourceDirs += file('src/test/integration/java')
        testSourceDirs += file('src/test/e2e/java')

        scopes.TEST.plus += [configurations.integrationCompile, configurations.integrationRuntime, configurations.e2eCompile, configurations.e2eRuntime]
    }
}